package ru.ulpfr.pension_brms

expander pens_rules.dsl

Подключить связанные классы

rule "1. Право на страховую пенсию имеют граждане Российской Федерации"

    when
        Клиент
        - является гражданином РФ
    then
        Добавить право на страховую пенсию по старости по критерию: национальнось
        //Добавить право на пенсию типа PENS_TYPE.Old_age по критерию: RIGHT_TYPES.NATIONALITY        
end

rule "2. Право на страховую пенсию по старости имеют мужчины, достигшие возраста 60 лет"
    when
    	Клиент
        - является мужчиной
        - возраст >= "Min_Age_Male"
    then
    	Добавить право на страховую пенсию по старости по критерию: возраст    
end

rule "3. Право на страховую пенсию по старости имеют женщины, достигшие возраста 55 лет"
	when
    	Клиент
        - является женщиной
        - возраст >= "Min_Age_Female"
    then
    	Добавить право на страховую пенсию по старости по критерию: возраст    
end

rule "4. Страховая пенсия по старости назначается при наличии не менее XX лет страхового стажа"
    when
    	Клиент
        - стаж работы >= "Min_Stag"
    then
    	Добавить право на страховую пенсию по старости по критерию: минимальный стаж
end

rule "5. Страховая пенсия по старости назначается при наличии величины индивидуального пенсионного коэффициента в размере не менее XX"
   	when
   		Клиент
        - Размер ИПК >= "Min_IPK"
    then
    	Добавить право на страховую пенсию по старости по критерию: минимальный ИПК
end

rule "6. Проверка, положена ли гражданину страxовая пенсия по всем обязательным критериям "
   	when
   		Клиент
   		Имеет запрос на пенсию
   		- вид пенсии: страховая по старости
   		- статус пенсии: не определен
        В наличии право для данного вида пенсии
        - по критерию: гражданство
       	В наличии право для данного вида пенсии
        - по критерию: возраст
        В наличии право для данного вида пенсии
        - по критерию: минимальный стаж
        В наличии право для данного вида пенсии
        - по критерию: минимальный ИПК
    then
        Обновить показатели пенсии:
        - статус пенсии: назначена
        - размер пенсии рассчитать по формуле: ИПК * СИПК
end

rule "7. Отменить страxовой пенсию, если не удовлетворяются все критерии "
   	when
        Клиент
   		Имеет запрос на пенсию
   		- вид пенсии: страховая по старости
   		- статус пенсии: не определен
        Отсутствует право для данного вида пенсии
        - по критерию: гражданство
        or
       	 Отсутствует право для данного вида пенсии
        - по критерию: возраст
        or
         Отсутствует право для данного вида пенсии
        - по критерию: минимальный стаж
        or
         Отсутствует право для данного вида пенсии
        - по критерию: минимальный ИПК    
    then
        Обновить показатели пенсии:
        - статус пенсии: отклонена
        - размер пенсии: 0   
end

rule "8. Отказ в страxовой пенсии по старости не гражданам РФ"
	when
        Клиент
        - не является гражданином РФ
        Имеет запрос на пенсию
   		- вид пенсии: страховая по старости
    then
        Добавить ошибку права на страховую пенсию по старости с формулировкой "ERR_1"
end

rule "9.1 Отказ в страxовой пенсии по старости по возрасту"
    when
       	Клиент
        - является мужчиной
        - возраст < "Min_Age_Male"
        Имеет запрос на пенсию
   		- вид пенсии: страховая по старости
    then
        Добавить ошибку права на страховую пенсию по старости с формулировкой "ERR_2" 
end

rule "9.2 Отказ в страxовой пенсии по старости по возрасту"
    when
        Клиент
        - является женщиной
        - возраст < "Min_Age_Female"
        Имеет запрос на пенсию
   		- вид пенсии: страховая по старости
    then
        Добавить ошибку права на страховую пенсию по старости с формулировкой "ERR_2" 
end

rule "10. Отказ в страxовой пенсии из-за недостаточного страxового стажа"
    when
        Клиент
        - стаж работы < "Min_Stag"
        Имеет запрос на пенсию
   		- вид пенсии: страховая по старости
    then
        Добавить ошибку права на страховую пенсию по старости с формулировкой "ERR_3" 
end

rule "11. Отказ в страxовой пенсии из-за недостаточного количества баллов"
    when
        Клиент
        - Размер ИПК < "Min_IPK"
        Имеет запрос на пенсию
   		- вид пенсии: страховая по старости
    then
        Добавить ошибку права на страховую пенсию по старости с формулировкой "ERR_4" 
end

rule "12. Вывод ошибки при назначении страxовой пенсии по старости"
    when
        Клиент
        Имеет ошибку права на страховую пенсию по старости
    then
    	Вывод: сообщение о причине отказа
end

rule "13. Вывод cообщения об успешном назначенной пенсии"
    when
        Клиент
        Имеет запрос на пенсию
   		- вид пенсии: страховая по старости
   		- статус пенсии: назначена
    then
    	Вывод: сообщение о назначенной пенсии
end



